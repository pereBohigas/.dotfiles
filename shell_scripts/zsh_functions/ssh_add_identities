#!/bin/zsh

# based on https://github.com/ohmyzsh/ohmyzsh/blob/master/plugins/ssh-agent/ssh-agent.plugin.zsh
local id file line sig lines
local -a identities loaded_sigs loaded_ids not_loaded

identities=("$@")

# check for .ssh directory presence
if [[ ! -d "$HOME/.ssh" ]]; then
	return
fi

# add default keys if no identities were set up via zstyle
# this is to mimic the call to ssh-add with no identities
if [[ ${#identities} -eq 0 ]]; then
	# key list found on `ssh-add` man page's DESCRIPTION section
	for id in id_rsa id_dsa id_ecdsa id_ed25519 identity; do
		# check if file exists
		[[ -f "$HOME/.ssh/$id" ]] && identities+=($id)
	done
fi

# get list of loaded identities' signatures and filenames
if lines=$(ssh-add -l); then
	for line in ${(f)lines}; do
		loaded_sigs+=${${(z)line}[2]}
		loaded_ids+=${${(z)line}[3]}
	done
fi

# add identities if not already loaded
for id in $identities; do
	# if id is an absolute path, make file equal to id
	[[ "$id" = /* ]] && file="$id" || file="$HOME/.ssh/$id"
	# check for filename match, otherwise try for signature match
	if [[ ${loaded_ids[(I)$file]} -le 0 ]]; then
		sig="$(ssh-keygen -lf "$file" | awk '{print $2}')"
		[[ ${loaded_sigs[(I)$sig]} -le 0 ]] && not_loaded+=("$file")
	fi
done

# abort if no identities need to be loaded
if [[ ${#not_loaded} -eq 0 ]]; then
return
fi

ssh-add --apple-use-keychain ${^not_loaded}

